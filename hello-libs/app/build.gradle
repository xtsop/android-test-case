apply plugin: 'com.android.application'

android {
    compileSdkVersion = 26 // 25
    buildToolsVersion = '26.0.0' //'25.0.2'

    defaultConfig {
        applicationId = 'com.example.hellolibs'
        minSdkVersion 13
        targetSdkVersion 25
        versionCode = 1
        versionName = '1.0'
        ndk {
            abiFilters 'x86', 'armeabi-v7a' // 'x86_64', 'armeabi', 'armeabi-v7a', 'arm64-v8a'
        }
        externalNativeBuild {
            cmake {
                arguments '-DANDROID_PLATFORM=android-13',
                        '-DANDROID_TOOLCHAIN=clang', '-DANDROID_STL=gnustl_static',
                        '-DOPENSSL_LIBRARIES=../distribution/openssl/lib'
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            // let gradle pack the shared library into apk
            jniLibs.srcDirs = ['../distribution/gperf/lib', '../distribution/openssl/lib']
        }
    }
    externalNativeBuild {
        cmake {
            path 'src/main/cpp/CMakeLists.txt'
        }
    }
}

configurations {
    iq
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:26.0.0' //'com.android.support:appcompat-v7:25.2.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.1'
    compile 'com.google.code.gson:gson:1.7.2'
    compile 'com.squareup.retrofit:retrofit:1.1.0'
    compile 'org.bouncycastle:bcprov-jdk15on:1.47'
    // uncomment out this one to generate lib binaries,
    // and also uncommented out the one in settings.gradle
    // after lib is generated, just comment them out again
//        compile project(':gen-libs')
    iq fileTree(dir: "/opt/IQ_CLI/", include: '*.jar') // added by RS
}

ext.iqScanDir = 'build/lib'
task copyCompileDependencies(type: Copy) {
    into iqScanDir
    from configurations.compile
}

task iqScan(type: JavaExec) {
    classpath = files(configurations.iq.asPath)

    main = 'com.sonatype.insight.scan.cli.PolicyEvaluatorCli'

    // arguments to pass to the IQ CLI
    args '-s', 'http://localhost:8070',
            '-i', 'AND1',
            '-a', "gradle_user:Dog1",
            iqScanDir
}
iqScan.dependsOn 'copyCompileDependencies'
